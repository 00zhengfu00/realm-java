apply plugin: 'groovy'
apply plugin: 'maven'

def props = new Properties()
props.load(new FileInputStream("${rootDir}/../realm.properties"))
props.each { key, val ->
    project.set(key, val)
}

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'io.realm'
version = file("${projectDir}/../version.txt").text.trim();

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "io.realm:realm-transformer:${version}"
    compile 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    compile 'com.android.tools.build:gradle:1.5.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

//for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/io/realm'
    filter(ReplaceTokens, tokens: [version: version])
    outputs.upToDateWhen { false }
}

// Include the generated Version file
sourceSets {
    main {
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

compileJava.dependsOn generateVersionClass

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}
